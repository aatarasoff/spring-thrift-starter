buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath "io.spring.gradle:dependency-management-plugin:0.5.5.RELEASE"
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3'
    }
}


allprojects {
    group = 'info.developerblog.spring.thrift'
    version = '0.9.0'

    repositories {
        jcenter()
    }
    
    apply plugin: 'idea'
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'nebula.provided-base'

    dependencyManagement {
        dependencies {
            dependencySet(group:'org.springframework.boot', version: '1.3.2.RELEASE') {
                entry "spring-boot-starter-web"
                entry "spring-boot-starter-test"
                entry "spring-boot-starter-actuator"
            }

            dependencySet(group:'org.springframework.cloud', version: '1.0.6.RELEASE') {
                entry "spring-cloud-starter-ribbon"
            }

            dependency "org.apache.thrift:libthrift:0.9.2"

            dependency "org.projectlombok:lombok:1.16.2"
        }
    }
    
    task resolveAllDependencies {
        doLast {
            configurations.all { it.resolve() }
        }
    }
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    test {
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
    }
    
    dependencies {
        provided "org.projectlombok:lombok"

        testCompile "org.springframework.boot:spring-boot-starter-test"
    }
}

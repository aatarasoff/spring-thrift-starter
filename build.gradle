buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.5.RELEASE")
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.+'
    }
}


allprojects {
    group = 'info.developerblog.spring.thrift'
    version = '1.0.0'

    repositories {
        jcenter()
    }

    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'provided-base'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Brixton.RELEASE"
        }

        dependencies {
            dependency 'org.projectlombok:lombok:1.16.8'
            dependency 'org.codehaus.groovy:groovy-all:2.4.5'
            dependency 'org.apache.thrift:libthrift:0.9.2'
            dependency 'com.squareup.okio:okio:1.8.0'
        }
    }

    task resolveAllDependencies {
        doLast {
            configurations.all { it.resolve() }
        }
    }
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    test {
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
    }

    dependencies {
        compile 'org.codehaus.groovy:groovy-all'
        provided "org.projectlombok:lombok"

        testCompile "org.springframework.boot:spring-boot-starter-test"
    }
}
